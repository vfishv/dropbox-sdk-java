buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath files('gradle/dropbox-pem-converter-plugin')
        classpath(dropboxJavaSdkLibs.android.gradle.plugin)
        classpath(dropboxJavaSdkLibs.kotlin.gradle.plugin)
        classpath("com.dropbox.gradle.plugins:stone-java-gradle-plugin")
    }
}

plugins {
    alias(dropboxJavaSdkLibs.plugins.maven.publish.plugin) apply false
    alias(dropboxJavaSdkLibs.plugins.gradle.version.plugin) apply false
    alias(dropboxJavaSdkLibs.plugins.dependency.guard) apply false
    alias(dropboxJavaSdkLibs.plugins.blind.pirate.osgi) apply false
    alias(dropboxJavaSdkLibs.plugins.binary.compatibility.validator) apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}

apply plugin: "com.dropbox.dependency-guard"

dependencyGuard {
    configuration("classpath")
}

// configure SDK to use the generated_stone_source output directory so we can track changes in Git
def sdkProject = findProject(':dropbox-sdk-java')
sdkProject.configure(sdkProject) {
    afterEvaluate {
        generateStone {
            outputDir "${project.projectDir}/generated_stone_source/main"
        }
        generateTestStone {
            outputDir "${project.projectDir}/generated_stone_source/test"
        }
    }
}
